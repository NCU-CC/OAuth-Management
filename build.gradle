apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse-wtp'

group = 'tw.edu.ncu.cc'
version = '0.0.1'

description = """NCU OAuth management"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {        
     maven { url "http://repo.maven.apache.org/maven2" }
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

dependencies {

	// springframework
    compile 'org.springframework:spring-orm:4.1.5.RELEASE'
    compile 'org.springframework:spring-tx:4.1.5.RELEASE'
	compile 'org.springframework:spring-webmvc:4.1.5.RELEASE'
	compile 'org.springframework:spring-context-support:4.1.6.RELEASE'
	compile 'jstl:jstl:1.2'
	compile 'javax.inject:javax.inject:1'
	
	// json
	compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4'
	
	// database
	compile 'com.zaxxer:HikariCP:2.3.5'
    compile 'mysql:mysql-connector-java:5.1.35'
	
	// hibernate
	compile 'org.hibernate:hibernate-core:4.3.8.Final'
    compile 'org.hibernate:hibernate-entitymanager:4.3.8.Final'
	compile 'org.hibernate:hibernate-validator:5.1.3.Final'
	
	// util
	compile 'org.apache.commons:commons-collections4:4.0'
	compile 'org.apache.commons:commons-lang3:3.3.2'
	compile 'commons-io:commons-io:2.4'
	compile 'joda-time:joda-time:2.7'
	compile 'com.google.guava:guava:18.0'
	
	// tiles
	compile 'org.apache.tiles:tiles-core:3.0.4'
	compile 'org.apache.tiles:tiles-jsp:3.0.4'
	
	// log
	compile 'org.slf4j:slf4j-log4j12:1.7.12'
	
	// security
	compile 'org.springframework.security:spring-security-openid:4.0.0.RELEASE'
	compile 'org.springframework.security:spring-security-config:4.0.0.RELEASE'
	compile 'org.springframework.security:spring-security-taglibs:4.0.0.RELEASE'
	
	// test
    compile 'org.springframework:spring-test:4.1.5.RELEASE'
	compile 'org.mockito:mockito-core:1.10.19'
    testCompile 'junit:junit:4.12'

    compile 'org.apache.tomcat:servlet-api:6.0.44'
	
	//integrationTestCompile 'junit:junit:4.12'
	//integrationTestCompile 'org.springframework:spring-test:4.1.5.RELEASE'
}

task integrationTest(type: Test) {
	testClassesDir = sourceSets.integrationTest.output.classesDir
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
	reports.html.destination = file("${reporting.baseDir}/${name}")
}